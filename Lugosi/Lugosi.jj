PARSER_BEGIN(Lugosi)
import java.io.*;
import java.util.ArrayList;

class ArvoreLugosi{
  ArrayList<Comando> comandos;
  ArrayList<Funcao> funcoes;

  ArvoreLugosi(ArrayList comandos, ArrayList funcoes){
    this.comandos = comandos;
    this.funcoes = funcoes;
  }
}

class Comando{}

class VarDecl extends Comando{
   String tipo;
   String id;

   VarDecl (String tipo, String id)
   {
    if (tipo.equals("bool")) {
      tipo = "boolean";
    }
    this.tipo=tipo;
    this.id=id;
   }

   public String toString() {
     return this.tipo + " " + this.id + ";";
   }
}

class Atrib extends Comando{
   String id;
   Exp exp = null;

   Atrib (String id, Exp exp)
   {
    this.id=id;
    this.exp=exp;
   }

   public String toString() {
     return this.id + " = " + this.exp + ";";
   }
}

class Condicional extends Comando{
    String condicional;
    Exp condicao;
    ArrayList<Comando> comandos;

    Condicional (String condicional, Exp condicao, ArrayList comandos)
    {
     this.condicional=condicional;
     this.condicao=condicao;
     this.comandos=comandos;
    }

    public String toString() {
      String s = this.condicional + " " + this.condicao + " {\n";

      for (Comando c : this.comandos) {
        s += "\t\t\t"+ c.toString() + "\n";
      }
      return  s + "\t\t}";
    }
}

class DoWhile extends Comando{
    Exp condicao;
    ArrayList<Comando> comandos;

    DoWhile (Exp condicao, ArrayList comandos)
    {
     this.condicao=condicao;
     this.comandos=comandos;
    }

    public String toString() {
      String s = "do {\n";

      for (Comando c : this.comandos) {
        s += "\t\t\t"+ c.toString() + "\n";
      }
      return  s + "\t\t} while " + this.condicao + ";";
    }
}

class funcaoChamadaCom extends Comando {
  String id;
  ListaExp exps;

  funcaoChamadaCom(String id, ListaExp exps)
  {
    this.id=id;
    this.exps=exps;
  }

  public String toString() {
     return this.id + this.exps + ");";
   }
}

class Return extends Comando {
  Exp exp;

  Return(Exp exp)
  {
    this.exp=exp;
  }

  public String toString() {
     return "return "+ this.exp + ";";
   }
}


class Exp{}

class Op extends Exp{
  String op;
  Exp e1;
  Exp e2;

  Op (String op, Exp e1, Exp e2)
  {
    this.op=op;
    this.e1=e1;
    this.e2=e2;
  }

  public String toString() {
    return "(" + this.e1 + " " + this.op + " " + this.e2 + ")";
  }
}

class Num extends Exp{
  String num;

  Num(String num)
  {this.num = num;}

  public String toString() {
     return this.num;
   }
}

class Bool extends Exp{
  boolean b;

  Bool(boolean b)
  {this.b = b;}

  public String toString() {
     return this.b
        ? "true"
        : "false";
   }
}

class Var extends Exp{
  String var;

  Var(String var)
  {this.var=var;}

  public String toString() {
     return this.var;
   }
}

class ListaExp extends Exp{
  ArrayList<Exp> exps;

  ListaExp(ArrayList exps)
  {this.exps=exps;}

  public String toString() {
      if (this.exps.size() == 0) {
        return "";
      }

      String s = "(";
      
      for (Exp e : this.exps) {
        s += e.toString() + ",";
      }
      
      return s.substring(0, s.length() - 1);
   }
}

class funcaoChamada extends Exp {
  String id;
  ListaExp exps;

  funcaoChamada(String id, ListaExp exps)
  {
    this.id=id;
    this.exps=exps;
  }

  public String toString() {
     if (this.exps.exps.size() == 0) {
       return this.id;
     }
     return this.id + this.exps + ")";
   }
}

class Arg{
  String tipo;
  String id;

  Arg(String tipo, String id)
  {
    if (tipo.equals("bool")) {
      tipo = "boolean";
    }
    this.tipo=tipo;
    this.id=id;
  }

  public String toString() {
     return this.tipo + " " + this.id;
   }
}

class Funcao{
  String tipo;
  String id;
  ArrayList<Arg> argumentos;
  ArrayList<Comando> comandos;

    Funcao (String tipo, String id, ArrayList argumentos, ArrayList comandos)
    {
      if (tipo.equals("bool")) {
        tipo = "boolean";
      }
     this.tipo=tipo;
     this.id=id;
     this.argumentos=argumentos;
     this.comandos=comandos;
    }

    public String toString() {
      String s = "\tpublic static " + this.tipo + " " + this.id + "(";

      for (Arg a : this.argumentos) {
        s += a.toString() + ",";
      }

      s = s.substring(0, s.length() - 1) + ") {\n";

      for (Comando c : this.comandos) {
        s += "\t\t"+ c.toString() + "\n";
      }

      return  s + "\t}\n";
    }
}

public class Lugosi {

  public static void main(String args[]) throws Exception {

    FileInputStream fs = new FileInputStream(new File(args[0]));

    Lugosi parser = new Lugosi(fs);

    ArvoreLugosi arvore = parser.Lugosi();

    geraCodigo(arvore, args[0]);
  }

  public static void geraCodigo(ArvoreLugosi prog, String arquivo){
    System.out.println("\npublic class Programa {");
    System.out.println("\tpublic static void main(String args[]) {");
    
    for (Comando c : prog.comandos) {
      System.out.print("\t\t");
      System.out.println(c);
    }
    System.out.println("\t}\n");

    for (Funcao f : prog.funcoes) {
      System.out.println(f);
    }
    System.out.println("}");
  }
}

PARSER_END(Lugosi)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

TOKEN :
{
  <MAIN: "main">
| <ACHAVES: "{">
| <FCHAVES: "}">
| <PONTOVIRGULA: ";">
| <VIRGULA: ",">
| <ATRIB: ":=">
| <APARENTESES: "(">
| <FPARENTESES: ")">
| <VAR: "var">
| <INT: "int">
| <BOOL: "bool">
| <FLOAT: "float">
| <VOID: "void">
| <TRUE: "true">
| <FALSE: "false">
| <IF: "if">
| <WHILE: "while">
| <DO: "do">
| <FUNC: "function">
| <RETURN: "return">
| <PRINT: "print">
}

TOKEN :
{
   <NUM : (["0"-"9"])+ ("."(["0"-"9"])+)? ("E" ("+" | "-")? (["0"-"9"])+)?>
 | <ID: (["a"-"z","A"-"Z"] ( ["a"-"z","A"-"Z","0"-"9"])* ("_" (["a"-"z","A"-"Z","0"-"9"])+)*)>
 | <OP: "+" | "-" | "*" |  "/" | "&&" | "||" | "<" | ">" | "==">

}

// LUGOSI -> MAIN FUNC?

ArvoreLugosi Lugosi () :
{ArrayList comandos= new ArrayList(); ArrayList funcoes= new ArrayList();}
{
  Main(comandos)
 (Func (funcoes)) ?
  <EOF> 
  {return new ArvoreLugosi(comandos, funcoes);}
}

// MAIN -> "main" "{" VARDECL SEQCOMANDOS "}"

void Main (ArrayList comandos) :
{}
{
  <MAIN> <ACHAVES> Vardecl (comandos) SeqComandos (comandos) <FCHAVES>
}

// TIPO -> "int" | "bool" | "float" | "void"
String Tipo () :
{Token tipo=null;}
{
  (tipo=<INT> | tipo=<BOOL> | tipo=<FLOAT> | tipo=<VOID>)
  {return tipo.image;}
}

// VARDECL -> VARDECL "var" TIPO TOKEN_id ";" | vazio
void Vardecl (ArrayList comandos) :
{String tipo; Token id=null; VarDecl vd;}
{
  <VAR> tipo=Tipo() id=<ID> {vd = new VarDecl(tipo, id.image);} <PONTOVIRGULA> Vardecl2 (comandos)
  {comandos.add(vd);}
} 

void Vardecl2 (ArrayList comandos) :
{String tipo; Token id=null; VarDecl vd;}
{
  (<VAR> tipo=Tipo() id=<ID> {vd = new VarDecl(tipo, id.image);} 
  {comandos.add(vd);} <PONTOVIRGULA> Vardecl2 (comandos)) | ({})
} 


// SEQCOMANDOS -> SEQCOMANDOS COMANDO | vazio
void SeqComandos (ArrayList comandos) :
{}
{
  Comando (comandos) SeqComandos (comandos) | {}
}

// COMANDO -> TOKEN_id ":=" EXP ";"
// | TOKEN_id "(" LISTAEXP? ")" ";"
// | "if" "(" EXP ")" "{" SEQCOMANDOS "}" ";"
// | "while" "(" EXP ")" "do" "{" SEQCOMANDOS "}" ";"
// | "do" "{" SEQCOMANDOS "}" "while" "(" EXP ")" ";"
// | "return" EXP ";"
// | "print" "(" EXP ")" ";"
void Comando (ArrayList comandos) :
{
  Comando c=null; 
  Token id=null; 
  Exp e=null; 
  ArrayList le=new ArrayList();
  ArrayList comandosCond=new ArrayList();
  }
{
  (id=<ID> c = Comando2 (id.image) |
  <IF> <APARENTESES> e=Exp () <FPARENTESES> <ACHAVES> SeqComandos (comandosCond) <FCHAVES> <PONTOVIRGULA> 
  {c = new Condicional("if", e, comandosCond);}|
  <WHILE> <APARENTESES> e=Exp () <FPARENTESES> <ACHAVES> SeqComandos (comandosCond) <FCHAVES> <PONTOVIRGULA> 
  {c = new Condicional("while", e, comandosCond);}|
  <DO> <ACHAVES> SeqComandos (comandosCond) <FCHAVES> <WHILE> <APARENTESES> e=Exp () <FPARENTESES> <PONTOVIRGULA> 
  {c = new DoWhile(e, comandosCond);}|
  <RETURN> e=Exp () <PONTOVIRGULA> {c = new Return(e);}|
  <PRINT> <APARENTESES> e=Exp () {le.add(e);} <FPARENTESES> <PONTOVIRGULA> 
  {c = new funcaoChamadaCom("System.out.println", new ListaExp(le));})
  {comandos.add(c);}
}

Comando Comando2 (String id) :
{Comando c=null; Exp e = null; ArrayList exps = new ArrayList();}
{
  ( <ATRIB> e = Exp () <PONTOVIRGULA>  {c = new Atrib (id, e);}
  | <APARENTESES> ListaExp (exps) <FPARENTESES> <PONTOVIRGULA> 
    {c = new funcaoChamadaCom (id, new ListaExp(exps));})
  
  {return c;}
}

// EXP -> "(" EXP OP EXP ")" | FATOR
Exp Exp () :
{Exp e=null; Exp e1=null; Exp e2=null;Token op=null;}
{
  (<APARENTESES> e1=Exp () op=<OP> e2=Exp () <FPARENTESES> {e = new Op(op.image, e1, e2);}| e=Fator ())
  {return e;}
}

// FATOR -> TOKEN_id | TOKEN_id "(" LISTAEXP? ")"
// | TOKEN_numliteral | "true" | "false"
Exp Fator () :
{Exp e=null; Token t=null; ArrayList exps=new ArrayList();}
{
  ( t=<NUM> {e = new Num(t.image);}
  | t=<ID> Fator2 (exps) {e = new funcaoChamada(t.image, new ListaExp(exps));}
  | t=<TRUE> {e = new Bool(true);}
  | t=<FALSE> {e = new Bool(false);})
  {return e;}
}

void Fator2 (ArrayList exps) :
{}
{
  <APARENTESES> ListaExp (exps) <FPARENTESES> | {}
}

// LISTAEXP -> EXP | LISTAEXP "," EXP
void ListaExp (ArrayList exps) :
{Exp e=null;}
{
  e = Exp () {exps.add(e);} ListaExp2 (exps)
}

void ListaExp2 (ArrayList exps) :
{Exp e=null;}
{
  <VIRGULA> e = Exp () {exps.add(e);} ListaExp2 (exps) | {}
}

// FUNC -> FUNC "function" TIPO TOKEN_id "(" LISTAARG? ")" "{" VARDECL SEQCOMANDOS "}"
// | "function" TIPO TOKEN_id "(" LISTAARG? ")" "{" VARDECL SEQCOMANDOS "}"
void Func (ArrayList funcoes) :
 {String tipo; Token id=null; ArrayList la=new ArrayList(); ArrayList comandosF=new ArrayList();}
 {
   <FUNC> tipo=Tipo () id=<ID> <APARENTESES> ListaArg (la) <FPARENTESES> <ACHAVES> 
   Vardecl (comandosF) SeqComandos (comandosF) <FCHAVES> {funcoes.add(new Funcao(tipo, id.image, la, comandosF));}
   Func2 (funcoes)
 }

 void Func2 (ArrayList funcoes) :
 {String tipo; Token id=null; ArrayList la=new ArrayList(); ArrayList comandosF=new ArrayList();}
 {
   <FUNC> tipo=Tipo () id=<ID> <APARENTESES> ListaArg (la) <FPARENTESES> <ACHAVES> 
   Vardecl (comandosF) SeqComandos (comandosF) <FCHAVES> 
   {funcoes.add(new Funcao(tipo, id.image, la, comandosF));}
   Func2 (funcoes) | {}
 }


// LISTAARG -> TIPO TOKEN_id | LISTAARG "," TIPO TOKEN_id
void ListaArg (ArrayList la) :
{String tipo; Token id=null;}
{
  tipo = Tipo () id = <ID> {la.add(new Arg(tipo, id.image));}
  ListaArg2 (la)  
}

void ListaArg2 (ArrayList la) :
{String tipo; Token id=null;}
{
  <VIRGULA> tipo = Tipo () id = <ID>
  {la.add(new Arg(tipo, id.image));} 
  ListaArg2 (la) | {}
}